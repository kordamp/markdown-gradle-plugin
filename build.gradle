/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2013-2022 Andres Almiray.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'java-library'
    id 'groovy'
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.plugin'
}

config {
    info {
        name          = rootProject.name
        description   = 'Markdown/HTML Gradle Plugin'
        inceptionYear = '2013'
        tags          = ['markdown']

        specification { enabled = false }

        people {
            person {
                id    = 'tednaleid'
                name  = 'Ted Naleid'
                roles = ['developer']
            }
            person {
                id    = 'madmas'
                name  = 'Markus Schlichting'
                roles = ['contributor']
            }
            person {
                id    = 'iruiz'
                name  = 'Ix-chel Ruiz'
                roles = ['contributor']
            }
        }
    }

    docs {
        groovydoc {
            replaceJavadoc = true
        }
    }

    plugins {
        plugin {
            name                = 'markdown'
            id                  = 'org.kordamp.gradle.markdown'
            implementationClass = 'org.kordamp.gradle.plugin.markdown.MarkdownPlugin'
        }
    }
}

repositories {
    gradlePluginPortal()
}

dependencies {
    compileOnly gradleApi()
    api('com.overzealous:remark:1.1.0') { transitive = false }
    api "org.jsoup:jsoup:$jsoupVersion"
    api "org.apache.commons:commons-lang3:$commonsLangVersion"
    api('org.pegdown:pegdown:1.6.0') { transitive = false }
    api "org.parboiled:parboiled-java:$parboiledVersion"
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'cglib:cglib-nodep:3.3.0'
}

processResources {
    inputs.property('version', project.version)
    filesMatching(['**/*.properties']) {
        expand(
            'version': project.version,
            'id': 'markdown',
            'name': 'Markdown Gradle plugin'
        )
    }
}